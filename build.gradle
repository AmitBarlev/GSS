plugins {
	id 'java'
	id 'idea'
	id 'io.gatling.gradle' version '3.13.3.1'
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.google.protobuf' version '0.9.4'
}

apply plugin: 'java'

group = 'com.abl'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/snapshot' }
}

ext {
	set('springGrpcVersion', "0.4.0-SNAPSHOT")
}

dependencies {

	compileOnly 'org.projectlombok:lombok:1.18.36'
	annotationProcessor 'org.projectlombok:lombok:1.18.36'

	implementation 'io.grpc:grpc-services'
	implementation 'org.springframework.grpc:spring-grpc-spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'

	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
	testImplementation 'org.junit.jupiter:junit-jupiter-api'
	testImplementation "org.testcontainers:junit-jupiter"

	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.projectlombok:lombok'

	//Spring test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.grpc:spring-grpc-test'
	testImplementation "io.grpc:grpc-inprocess"

	//Test containers
	implementation platform('org.testcontainers:testcontainers-bom:1.20.4')
	testImplementation "org.testcontainers:testcontainers"
	testImplementation "org.testcontainers:mongodb"

	gatlingImplementation("io.gatling:gatling-grpc-java:3.13.3.1")
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.grpc:spring-grpc-dependencies:${springGrpcVersion}"
	}
}

protobuf {
	protoc {
		artifact = 'com.google.protobuf:protoc'
	}
	plugins {
		grpc {
			artifact = 'io.grpc:protoc-gen-grpc-java'
		}
	}
	generateProtoTasks {
		ofSourceSet("gatling").forEach { task ->
			compileGatlingJava.dependsOn(task)
			task.plugins {
				grpc {}
			}
		}

		all()*.plugins {
			grpc {
				option 'jakarta_omit'
				option '@generated=omit'
			}
		}
	}
}

var generatedSources = [
		file("${protobuf.generatedFilesBaseDir}/gatling/java"),
		file("${protobuf.generatedFilesBaseDir}/gatling/grpc")
]

idea {
	module {
		generatedSources.forEach { generatedSourceDirs += it }
	}
}

sourceSets {
	gatling {
		java {
			generatedSources.forEach { srcDirs += it }
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

